apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.jfrog.bintray"

version = "1.0.3"

def siteUrl = 'https://github.com/saantiaguilera/android-api-resources_on_demand'
def gitUrl = 'https://github.com/saantiaguilera/android-api-resources_on_demand.git'
group = "com.saantiaguilera"

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'

                name 'com.saantiaguilera:Pomu'
                description = 'Download resources on demand'
                url siteUrl

                licenses {
                    license {
                        name 'MIT'
                    }
                }
                developers {
                    developer {
                        id 'saantiaguilera'
                        name 'Santiago Aguilera'
                        email 'marquito.santi@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += configurations.javadocDeps
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "com.saantiaguilera:Pomu"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['MIT']
        publish = true
    }
}